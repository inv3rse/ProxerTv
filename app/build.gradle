apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'com.getkeepsafe.dexcount'
apply plugin: 'io.fabric'

buildscript {
    repositories {
        mavenCentral()
    }

    dependencies {
        classpath 'com.getkeepsafe.dexcount:dexcount-gradle-plugin:0.6.0'
    }
}

// get api key from properties ore environment variable
def apiKey = ""
if (hasProperty("ProxerApiKey")) {
    apiKey = ProxerApiKey
} else {
    apiKey = System.getenv("ProxerApiKey")
}

final signing = 'com.inverse.unofficial.proxertv'
if (hasProperty(signing)) {
    apply from: property(signing)
}

android {
    compileSdkVersion 24
    buildToolsVersion "24.0.2"

    defaultConfig {
        applicationId "com.inverse.unofficial.proxertv"
        minSdkVersion 21
        targetSdkVersion 24
        versionCode 7
        versionName "Alpha 7"

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        buildConfigField("String", "PROXER_API_KEY", apiKey)
    }
    buildTypes {
        debug {
            applicationIdSuffix '.debug'
            minifyEnabled false
        }
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            if (hasProperty("signingConfigs.release")) {
                signingConfig signingConfigs.release
            }
        }
    }
    sourceSets {
        test.resources.srcDirs += 'src/test/resources/'
        test.java.srcDirs += 'src/testUtils/java'
        androidTest.java.srcDirs += 'src/testUtils/java'
    }

    lintOptions {
        lintConfig file("lint.xml")
    }
}

// keep generated classes
kapt {
    generateStubs = true
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])

    // Kotlin
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"

    // Support Library
    def support_library_version = '24.2.1'
    compile "com.android.support:leanback-v17:$support_library_version"

    // Glide
    compile 'com.github.bumptech.glide:glide:3.7.0'

    // RxJava
    compile 'io.reactivex:rxjava:1.1.10'
    compile 'io.reactivex:rxandroid:1.2.1'

    // OkHttp
    def okhttp_version = '3.4.1'
    compile "com.squareup.okhttp3:okhttp:$okhttp_version"
    compile "com.squareup.okhttp3:logging-interceptor:$okhttp_version"
    compile 'com.github.franmontiel:PersistentCookieJar:v0.9.3'

    // Retrofit
    def retrofit_version = "2.1.0"
    compile "com.squareup.retrofit2:retrofit:$retrofit_version"
    compile "com.squareup.retrofit2:converter-gson:$retrofit_version"
    compile "com.squareup.retrofit2:adapter-rxjava:$retrofit_version"

    // Kotlin Gson
    compile 'com.github.salomonbrys.kotson:kotson:2.2.1'

    // BeautifulSoup
    compile 'org.jsoup:jsoup:1.9.2'

    // ExoPlayer
    compile 'com.google.android.exoplayer:exoplayer:r1.5.10'

    // Dagger 2
    def dagger_version = '2.6.1'
    compile "com.google.dagger:dagger:$dagger_version";
    kapt "com.google.dagger:dagger-compiler:$dagger_version"
    compile "javax.annotation:javax.annotation-api:1.2" // pick up generated classes

    // Anko
    def anko_version = '0.8.3'
    compile "org.jetbrains.anko:anko-common:$anko_version"
    compile "org.jetbrains.anko:anko-sqlite:$anko_version"

    // PaperParcel
    def paperparcel_version = '1.0.0'
    compile "com.github.grandstaish.paperparcel:paperparcel:$paperparcel_version"
    compile "com.github.grandstaish.paperparcel:paperparcel-kotlin:$paperparcel_version"
    kapt "com.github.grandstaish.paperparcel:compiler:$paperparcel_version"

    // Timber
    compile 'com.jakewharton.timber:timber:4.1.2'

    // Crashlytics
    compile('com.crashlytics.sdk.android:crashlytics:2.6.2@aar') {
        transitive = true;
    }

    // Testing
    testCompile 'junit:junit:4.12'
    testCompile "com.squareup.okhttp3:mockwebserver:$okhttp_version"
    androidTestCompile('com.android.support.test:runner:0.5') {
        exclude module: 'support-annotations'
    }
    androidTestCompile('com.android.support.test:rules:0.5') {
        exclude module: 'support-annotations'
    }
}