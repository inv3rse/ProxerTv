apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'io.fabric'

// get api key from properties or environment variable
def apiKey = ""
if (hasProperty("ProxerApiKey")) {
    apiKey = ProxerApiKey
} else {
    apiKey = System.getenv("ProxerApiKey")
}

final signing = 'com.inverse.unofficial.proxertv'
if (hasProperty(signing)) {
    apply from: property(signing)
}

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.2"

    defaultConfig {
        applicationId "com.inverse.unofficial.proxertv"
        minSdkVersion 21
        targetSdkVersion 25
        versionCode 10
        versionName "Alpha 10"

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        buildConfigField("String", "PROXER_API_KEY", apiKey)
    }
    buildTypes {
        debug {
            applicationIdSuffix '.debug'
            minifyEnabled false
        }
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    sourceSets {
        test.resources.srcDirs += 'src/test/resources/'
        test.java.srcDirs += 'src/testUtils/java'
        androidTest.java.srcDirs += 'src/testUtils/java'
    }

    lintOptions {
        lintConfig file("lint.xml")
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }
}

// keep generated classes
kapt {
    generateStubs = true
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    // Kotlin
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jre7:$kotlin_version"

    // Support Library
    def support_library_version = '25.4.0'
    implementation "com.android.support:leanback-v17:$support_library_version"
    implementation "com.android.support:percent:$support_library_version"

    // Glide
    implementation 'com.github.bumptech.glide:glide:3.7.0'

    // RxJava
    implementation 'io.reactivex:rxjava:1.2.5'
    implementation 'io.reactivex:rxandroid:1.2.1'

    // OkHttp
    def okhttp_version = '3.5.0'
    implementation "com.squareup.okhttp3:okhttp:$okhttp_version"
    implementation "com.squareup.okhttp3:logging-interceptor:$okhttp_version"
    implementation 'com.github.franmontiel:PersistentCookieJar:v0.9.3'

    // Retrofit
    def retrofit_version = "2.1.0"
    implementation "com.squareup.retrofit2:retrofit:$retrofit_version"
    implementation "com.squareup.retrofit2:converter-gson:$retrofit_version"
    implementation "com.squareup.retrofit2:adapter-rxjava:$retrofit_version"

    // Kotlin Gson
    implementation 'com.github.salomonbrys.kotson:kotson:2.5.0'

    // BeautifulSoup
    implementation 'org.jsoup:jsoup:1.9.2'

    // ExoPlayer
    implementation('com.google.android.exoplayer:exoplayer:r2.4.0')
    implementation("com.android.support:support-compat:$support_library_version")
    implementation("com.android.support:support-core-utils:$support_library_version")

    // Dagger 2
    def dagger_version = '2.8'
    implementation "com.google.dagger:dagger:$dagger_version"
    compileOnly "javax.annotation:javax.annotation-api:1.2" // pick up generated classes
    kapt "com.google.dagger:dagger-compiler:$dagger_version"

    // Anko
    def anko_version = '0.9.1a'
    compile "org.jetbrains.anko:anko-common:$anko_version"
    compile "org.jetbrains.anko:anko-sqlite:$anko_version"

    // PaperParcel
    def paperparcel_version = '1.0.0'
    implementation "com.github.grandstaish.paperparcel:paperparcel:$paperparcel_version"
    implementation "com.github.grandstaish.paperparcel:paperparcel-kotlin:$paperparcel_version"
    kapt "com.github.grandstaish.paperparcel:compiler:$paperparcel_version"

    // Timber
    implementation 'com.jakewharton.timber:timber:4.5.1'

    // Crashlytics
    implementation('com.crashlytics.sdk.android:crashlytics:2.6.8@aar') {
        transitive = true
    }

    // Testing
    testImplementation 'junit:junit:4.12'
    testImplementation "com.squareup.okhttp3:mockwebserver:$okhttp_version"
    testImplementation "com.nhaarman:mockito-kotlin:1.5.0"
    androidTestImplementation "com.squareup.okhttp3:mockwebserver:$okhttp_version"
    androidTestImplementation('com.android.support.test:runner:0.5') {
        exclude module: 'support-annotations'
    }
    androidTestImplementation('com.android.support.test:rules:0.5') {
        exclude module: 'support-annotations'
    }
}